apply plugin: 'java'

sourceCompatibility = '1.8'
version = '0.9.0-SNAPSHOT'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'com.frinika.FrinikaApp'
    ext.appArgs = '-dev'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    doFirst {
        options.fork = true
        options.bootClasspath = "${System.properties['java.home']}/lib/rt.jar"
        options.bootClasspath += "$File.pathSeparator${System.properties['java.home']}/jre/lib/jsse.jar"
    }
}

// Disable tests on build
check.dependsOn.remove(test)

task testJar(type: Jar, dependsOn: testClasses) {
    classifier "tests"
    baseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    tests testJar
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def configureExec(task) {
    task.main = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
//    task.args appArgs
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    if (!gradle.startParameter.taskNames.contains("offline")) {
        dependencies {
            classpath("at.bxm.gradleplugins:gradle-svntools-plugin:latest.integration")
            classpath "gradle.plugin.edu.sc.seis.gradle:launch4j:2.4.1"
        }
    }
}

['edu.sc.seis.launch4j'].each {
    apply plugin: it
}

repositories {
    mavenCentral()
    flatDir {
        dirs "../../lib"
    }
}

dependencies {
    compile(project(':modules:frinika-core'))
    compile(project(':modules:frinika-main'))
    compile(project(':modules:frinika-mod'))
    compile(project(':modules:frinika-radio'))
    compile(project(':modules:frinika-view-notation'))
    compile(project(':modules:frinika-sequencer'))
    compile(project(':modules:frinika-project'))
    compile(project(':modules:frinika-driver-frogdisco'))
    compile(project(':modules:frinika-driver-jack'))
    compile(project(':modules:frinika-driver-osx'))
    compile(project(':modules:frinika-device-synthrack'))
    compile(project(':modules:frinika-device-rasmusdsp'))
    compile(project(':modules:frinika-device-opl3'))
    compile(project(':modules:frinika-device-debug'))
    compile(project(':modules:frinika-mystudio'))
    compile(project(':modules:frinika-audio-vorbis'))
    compile 'com.apple:AppleJavaExtensions:1.4'
    compile 'com.jgoodies:looks:2.1.4'

    runtime ':gervill'
    runtime 'org.codehaus.groovy:groovy-all:2.0.1'
    runtime ':jjack'
    runtime ':JVstHost'
    runtime ':Logging'
    runtime ':MRJAdapter'
    runtime 'com.apple:AppleJavaExtensions:1.4'
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

launch4j {
    outfile = "frinika.exe"
    icon = "../../../../images/frinika.ico"
    mainClassName = "com.frinika.FrinikaApp"
    jar = "../../build/libs/frinika-app-0.9.0-SNAPSHOT.jar"
    copyright = "Frinika Project"
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': configurations.runtime.collect {'lib/' + it.getName() }.join(' ')
        )
    }
}
