// Shared modules build

version = '0.9.0-SNAPSHOT'
def repositoryName = "frinika"
def libraryUrl = 'http://frinika.com'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs "../../lib"
    }
}

['java', 'maven'].each {
    apply plugin: it
}

sourceCompatibility = '1.8'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
//    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    doFirst {
        options.fork = true
        options.bootClasspath = "${System.properties['java.home']}/lib/rt.jar"
        options.bootClasspath += "$File.pathSeparator${System.properties['java.home']}/jre/lib/jsse.jar"
    }
}

// Disable tests on build
check.dependsOn.remove(test)

task testJar(type: Jar, dependsOn: testClasses) {
    classifier "tests"
    baseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    tests testJar
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    testCompile group: 'junit', name: 'junit', version: '4.10'
}
